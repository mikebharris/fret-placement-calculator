resource "aws_iam_role" "api_iam_role" {
  name                  = "${var.product}-api-iam-role"
  force_detach_policies = true
  assume_role_policy    = jsonencode({
    Version   = "2012-10-17"
    Statement = [
      {
        Action    = "sts:AssumeRole"
        Effect    = "Allow"
        Sid       = ""
        Principal = {
          Service = "lambda.amazonaws.com"
        }
      }
    ]
  })
}

resource "aws_cloudwatch_log_group" "api_cloudwatch_log_group" {
  name              = "/aws/lambda/${aws_lambda_function.api_lambda_function.function_name}"
  retention_in_days = 1
}

data "aws_iam_policy_document" "api_iam_policy_document" {
  statement {
    effect  = "Allow"
    actions = [
      "ec2:DescribeNetworkInterfaces",
    ]
  }
}

resource "aws_iam_role_policy_attachment" "api_policy_attachment_execution" {
  role       = aws_iam_role.api_iam_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaBasicExecutionRole"
}

data "archive_file" "api_lambda_function_distribution" {
  source_file = "../lambdas/fret-placement-calculator-api/bootstrap"
  output_path = "../lambdas/fret-placement-calculator-api/${var.product}-api.zip"
  type        = "zip"
}

resource "aws_s3_object" "api_lambda_function_distribution_bucket_object" {
  bucket = var.distribution_bucket
  key    = "lambdas/${var.product}-api/${var.product}-api.zip"
  source = data.archive_file.api_lambda_function_distribution.output_path
  etag   = filemd5(data.archive_file.api_lambda_function_distribution.output_path)
}

resource "aws_lambda_function" "api_lambda_function" {
  function_name    = "${var.product}-api"
  role             = aws_iam_role.api_iam_role.arn
  handler          = "bootstrap"
  runtime          = "provided.al2023"
  architectures    = ["arm64"]
  s3_bucket        = aws_s3_object.api_lambda_function_distribution_bucket_object.bucket
  s3_key           = aws_s3_object.api_lambda_function_distribution_bucket_object.key
  source_code_hash = data.archive_file.api_lambda_function_distribution.output_md5
  timeout          = 15
  memory_size      = 128

  tags = {
    Name          = "${var.product}.lambda.api"
    Contact       = var.contact
    Project       = var.product
    Orchestration = var.orchestration
    Description   = "API for calculating and returning fret placements"
  }
}

resource "aws_lambda_function_url" "api_lambda_function_url" {
  authorization_type = "NONE"
  function_name      = aws_lambda_function.api_lambda_function.function_name
}


